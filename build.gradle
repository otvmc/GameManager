plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'java'
    id 'maven-publish'
}

group 'cc.lynzie'
version '1.0.0'

repositories {
    mavenCentral()
    maven { url "https://repo.aikar.co/content/groups/aikar/" }
    maven { url "https://papermc.io/repo/repository/maven-public/" }

    maven {
        url = uri("https://maven.pkg.github.com/otvmc/GameManager")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT"
    implementation "co.aikar:acf-paper:0.5.0-SNAPSHOT"
}

shadowJar {
    relocate 'co.aikar.commands', 'cc.lynzie.minigame.acf'
    relocate 'co.aikar.locale', 'cc.lynzie.minigame.acf.locales'
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/otvmc/GameManager")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            from components.java
        }
    }
}